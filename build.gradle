buildscript {
    ext {
        kotlin_version = '1.5.21'
        junit_version = '4.12'
        mockito_version = '3.12.4'
        jgoodies_version = '1.6.0'
        json_version = '20180813'
        commonsio_version = '2.6'
        apache_commons_version = '3.0'
        flatlaf_version = '1.5'
        jacoco_tool_version = '0.8.7'
        shadowjar_version = '6.1.0'
        testlogger_version = '1.7.0'
        coberalls_version = '2.8.4'
    }

    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version "$shadowjar_version"
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id 'com.adarshr.test-logger' version "$testlogger_version"
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version "$coberalls_version"
}

jacoco {
    toolVersion = "$jacoco_tool_version"
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
    afterEvaluate {
        // Exclude files that cannot be tested by jUnit (UI, Swing, ...)
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/tibagni/logviewer/AppInfo*',
                    'com/tibagni/logviewer/LogViewerApplication*',
                    'com/tibagni/logviewer/LogViewerView*',
                    'com/tibagni/logviewer/MainView*',
                    'com/tibagni/logviewer/ServiceLocator*',
                    'com/tibagni/logviewer/VisibleLogsConfigurationDialog*',
                    'com/tibagni/logviewer/about/AboutDialog*',
                    'com/tibagni/logviewer/bugreport/BugReportView*',
                    'com/tibagni/logviewer/bugreport/section/ui*',
                    'com/tibagni/logviewer/filter/EditFilterDialog*',
                    'com/tibagni/logviewer/filter/FiltersList*',
                    'com/tibagni/logviewer/filter/FilterCellRenderer*',
                    'com/tibagni/logviewer/filter/regex/RegexEditorDialog*',
                    'com/tibagni/logviewer/log/LogCellRenderer*',
                    'com/tibagni/logviewer/logger/*',
                    'com/tibagni/logviewer/preferences/LogViewerPreferencesDialog*',
                    'com/tibagni/logviewer/theme/*',
                    'com/tibagni/logviewer/updates/UpdateAvailableDialog*',
                    'com/tibagni/logviewer/util/SwingUtils*',
                    'com/tibagni/logviewer/util/layout/*',
                    'com/tibagni/logviewer/util/scaling/*',
                    'com/tibagni/logviewer/view/*'
            ])
        }))
    }
}

def props = new Properties()
file("src/main/resources/properties/app.properties").withInputStream { props.load(it) }

version props.getProperty("version")

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

jar {
    manifest {
        attributes(
                'Built-By'       : System.properties['user.name'],
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By'     : "Gradle ${gradle.gradleVersion}",
                'Build-Jdk'      : "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS'       : "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Main-Class'     : "com.tibagni.logviewer.LogViewerApplication",
                'SplashScreen-Image' : "Images/splash.png"
        )
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: "$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    implementation group: 'com.jgoodies', name: 'jgoodies-forms', version: "$jgoodies_version"
    implementation group: 'org.json', name: 'json', version: "$json_version"
    implementation group: 'commons-io', name: 'commons-io', version: "$commonsio_version"
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: "$apache_commons_version"
    implementation group: 'com.formdev', name: 'flatlaf', version: "$flatlaf_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}
